plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "edu.wpi.first.GradleRIO" version "2019.4.1" apply false
}

allprojects {
	version "1.0.0"
}

subprojects {
    apply {
        plugin("java")
        plugin("maven")
        plugin("maven-publish")
    }
    
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    task sourcesJar(type: Jar, dependsOn: classes) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}
	
	task javadocJar(type: Jar, dependsOn: javadoc) {
	    classifier = 'javadoc'
	    from javadoc.destinationDir
	}
	
	artifacts {
	    archives sourcesJar
	    archives javadocJar
	}
}

javadoc {
    source subprojects.collect { project ->
        project.sourceSets.main.allJava
    }
    
    destinationDir = new File(buildDir, 'javadoc')
    
    classpath = files(subprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

wrapper {
    gradleVersion = "5.5.1"
    distributionType = Wrapper.DistributionType.BIN
}